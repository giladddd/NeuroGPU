create soma
 soma {
     	L = 30
      nseg = 10						
      diam(	0	:	0.1	) = 	5	:	10
      diam(	0.1	:	0.2	) = 	10	:	14
      diam(	0.2	:	0.3	) = 	14	:	16.2
      diam(	0.3	:	0.4	) = 	16.2	:	18
      diam(	0.4	:	0.5	) = 	18	:	19
      diam(	0.5	:	0.6	) = 	19	:	20
      diam(	0.6	:	0.7	) = 	20	:	19
      diam(	0.7	:	0.8	) = 	19	:	16
      diam(	0.8	:	0.9	) = 	16	:	10
      diam(	0.9	:	1	) = 	10	:	6
      
  
	  } 
n_axon_seg = 5
create iseg,hill,myelin[n_axon_seg],node[n_axon_seg]
//load_file("P_Axon.hoc")
//create_axon_hu()
load_file("nrngui.hoc")
proc create_axon() {
	
	

	soma {
		equiv_diam = sqrt(area(.5)/(4*PI))
	}

	iseg {               // initial segment between hillock + myelin
		pt3dclear() pt3dadd(0,0,0,diam) pt3dadd(0,-1000,0,diam)
		L = 10
		nseg = 5
		diam = equiv_diam/10        // see Sloper and Powell 1982, Fig.71
	}

	hill {                
		pt3dclear() pt3dadd(0,0,0,diam) pt3dadd(0,-1000,0,diam) 
		L = 20
		nseg = 5
		diam(0:1) = 4*iseg.diam:iseg.diam
	}

	// construct myelinated axon with nodes of ranvier

	for i=0,n_axon_seg-1 {
		myelin[i] {         // myelin element
			pt3dclear() pt3dadd(0,0,0,diam) pt3dadd(0,-1000,0,diam)
			nseg = 5
			L = 100
		diam = iseg.diam
		}
		node[i] {           // nodes of Ranvier
			pt3dclear() pt3dadd(0,0,0,diam) pt3dadd(0,-1000,0,diam)
			nseg = 1
			L = 1.0
			diam = iseg.diam*.75       // nodes are thinner than axon
		}
	}
	
	soma connect hill(0), 0.5
	hill connect iseg(0), 1
	iseg connect node[0](0), 1
	node[0] connect myelin[0](0), 1

	for i=0,n_axon_seg-2  { 
		myelin[i] connect node[i+1](0), 1
		node[i+1] connect myelin[i+1](0), 1 
	}
}

// axon model based mainly on Kole's model 2012
proc density() {
	if(VoltageClampMode==0){ 
		// exceptions along the axon
		forsec "myelin" cm = cm_myelin
		forsec "node" g_pas = g_pas_node
		forall vshift_na = -5
		forsec "myelin" {
		insert Kv7 		gbar_Kv7 = 1 
		insert Kv11 	gbar_Kv11 = 5
		insert Kv12 	gbar_Kv12 = 20
		insert nax  	gbar_nax = 40
		}   
		
		hill {
			insert Kv7
			insert na12
			gbar_na12 = gna_iseg
			vshift_na = shift_na_axon
			gbar_Kv7 =  gkv7_axon
		}	
		
		iseg {
			insert nax	gbar_nax(0:0.4) = 0:gnax_iseg
						gbar_nax(0.4:1) = gnax_iseg:gnax_iseg
						vshiftm_nax = shift_nax_act_axon
						vshifth_nax = shift_nax_inact_axon
			insert na12 gbar_na12(0:0.2) = gna_iseg:gna_axon
						gbar_na12(0.2:0.4) = gna_axon:0
						vshift_na = shift_na_axon
						insert Kv7 	gbar_Kv7(0:0.4) = gkv7_axon:gkv7_axon
						gbar_Kv7(0.4:1) = gkv7_axon:gkv7_iseg
			insert Kv11 gbar_Kv11(0:1)= gkv11_axon:gkv11_iseg	
			insert Kv12 gbar_Kv12(0:1)= gkv12_axon:gkv12_iseg	
		}
		
		forsec "node" {
			insert Kv7
			insert Kv11
			insert Kv12
			insert nax
			gbar_Kv11 = gkv11_iseg
			gbar_Kv12 = gkv12_iseg
			gbar_Kv7 = gkv7_iseg
			gbar_nax = gnax_node
			vShift_nax = shift_nax_act_axon
			vShift_inact_nax = shift_nax_inact_axon
		}
	}
}

//axon parameters guessed by mara
gkv11_axon=100
gkv11_iseg=1500
gkv12_axon=500
gkv12_iseg=3000
gkv7_axon=1
gkv7_iseg=10
gnax_node=30000
gnax_iseg=30000
gna_iseg=50000
gna_axon=5000
shift_na_axon=-10
shift_nax_act_axon=10
shift_nax_inact_axon=10
create_axon()